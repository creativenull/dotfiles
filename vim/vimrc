" Name: Arnold Chand
" Github: https://github.com/creativenull
" Description: My vimrc config, aiming to be one file, self-contained.
" =============================================================================
" User Configuration (USER)
" =============================================================================

let mapleader = "\<Space>"

let s:undopath = expand('$HOME/.cache/vim/undo')
if isdirectory(s:undopath) == 0
	echomsg 'Creating undodir directory'
	call mkdir(s:undopath, 'p')
endif

" =============================================================================
" Options (OPT)
" =============================================================================

unlet! skip_defaults_vim
source $VIMRUNTIME/defaults.vim

let &titlestring=printf("%s [vim]", fnamemodify(getcwd(), ":t"))
let &undodir=expand('$HOME/.cache/vim/undo')
set backspace=indent,eol,start
set clipboard-=autoselect
set cmdheight=2
set colorcolumn=80,120
set completeopt=menuone,noinsert,noselect,fuzzy
set conceallevel=2
set encoding=utf-8
set guicursor=n-v-c-sm:block,i-ci-ve:block,r-cr-o:hor20
set hidden
set history=10000
set hlsearch
set ignorecase
set incsearch
set laststatus=2
set lazyredraw
set mouse=nv
set nobackup
set noexpandtab
set nofoldenable
set nospell
set noswapfile
set nowrap
set number
set path=**
set scrolloff=1
set shiftwidth=4
set shortmess+=c
set showmatch
set showtabline=0
set signcolumn=yes
set smartcase
set smartindent
set softtabstop=4
set tabstop=4
set termguicolors
set title
set ttimeoutlen=50
set undofile
set undolevels=10000
set updatetime=250
set wildignore=*.git/*,*node_modules/*,*vendor/*,*dist/*,*build/*
set wildignorecase
set wildmenu
set wildoptions=pum,fuzzy

if has('termguicolors')
	set termguicolors
	let &t_8f = "\<Esc>[38;2;%lu;%lu;%lum"
	let &t_8b = "\<Esc>[48;2;%lu;%lu;%lum"
endif

" =============================================================================
" Keymaps (KEY)
" =============================================================================

nnoremap <Up> <Nop>
nnoremap <Down> <Nop>
nnoremap <Left> <Nop>
nnoremap <Right> <Nop>
inoremap <Up> <Nop>
inoremap <Down> <Nop>
inoremap <Left> <Nop>
inoremap <Right> <Nop>

nnoremap <C-k> <Nop>
nnoremap <C-j> <Nop>
nnoremap <C-h> <Nop>
nnoremap <C-l> <Nop>
inoremap <C-k> <Nop>
inoremap <C-j> <Nop>
inoremap <C-h> <Nop>
inoremap <C-l> <Nop>

nnoremap <Up> <Cmd>call <SID>ResizeHorizontal(2)<CR>
nnoremap <Down> <Cmd>call <SID>ResizeHorizontal(-2)<CR>
nnoremap <Left> <Cmd>call <SID>ResizeVertical(2)<CR>
nnoremap <Right> <Cmd>call <SID>ResizeVertical(-2)<CR>

" Map escape from terminal input to Normal mode
tnoremap <Esc> <C-\><C-n>
tnoremap <C-[> <C-\><C-n>

" Disable highlights
nnoremap <Leader><CR> <Cmd>noh<CR>

" Close all buffer, except current
nnoremap <Leader>bx <Cmd>%bd<Bar>e#<Bar>bd#<CR>

" Move a line of text Alt+[j/k]
nnoremap <M-j> mz:m+<CR>`z
vnoremap <M-j> :m'>+<CR>`<my`>mzgv`yo`z
nnoremap <M-k> mz:m-2<CR>`z
vnoremap <M-k> :m'<-2<CR>`>my`<mzgv`yo`z

" Edit vimrc
nnoremap <Leader>ve <Cmd>edit $MYVIMRC<CR>

" Source the vimrc to reflect changes
nnoremap <Leader>vs <Cmd>source $MYVIMRC<CR>

" Reload file
nnoremap <Leader>r <Cmd>edit!<CR>

" Copy/Paste from sytem clipboard
vnoremap p "_dP
vnoremap <Leader>y "+y
nnoremap <Leader>y "+y
nnoremap <Leader>p "+p

" =============================================================================
" Commands (CMD)
" =============================================================================

" Command Abbreviations, I can't release my shift key fast enough ðŸ˜­
cnoreabbrev Q  q
cnoreabbrev Qa qa
cnoreabbrev W  w
cnoreabbrev Wq wq

" =============================================================================
" Functions (FUN)
" =============================================================================

function! s:ResizeHorizontal(amount) abort
	let l:wincount = getwininfo()->len()
	if l:wincount > 1
		call execute(printf('resize %s%d', a:amount > 0 ? '+' : '', a:amount))
	endif
endfunction

function! s:ResizeVertical(amount) abort
	let l:wincount = getwininfo()->len()
	if l:wincount > 1
		call execute(printf('vertical resize %s%d', a:amount > 0 ? '+' : '', a:amount))
	endif
endfunction

function! s:VimGrep(qargs, bang) abort
	let s:sh = 'rg --column --line-number --no-heading --color=always --smart-case -- ' .. shellescape(a:qargs)
	call fzf#vim#grep(s:sh, 1, fzf#vim#with_preview('right:50%', 'ctrl-/'), a:bang)
endfunction

function! s:FzfStatusline()
	" Override statusline as you like
	highlight fzf1 guibg=NONE
	setlocal statusline=%#fzf1#\ >\ fzf
endfunction

function! s:FernInit() abort
	nnoremap q <Cmd>bd<CR>
	nmap <buffer> D <Plug>(fern-action-remove)
	setlocal expandtab shiftwidth=2 tabstop=2
	call glyph_palette#apply()
endfunction

function! s:SetLightTheme() abort
	set background=light
	colorscheme one
endfunction

function! s:SetDarkTheme() abort
	set background=dark
	colorscheme moonfly
endfunction

function! s:UpdateGuiMacvimColorscheme()
	if v:os_appearance == 0
		call <SID>SetLightTheme()
	else
		call <SID>SetDarkTheme()
	endif
endfunction

function! s:ConfigTheme() abort
	if has('gui_macvim')
		call <SID>UpdateGuiMacvimColorscheme()

		" Register event to automatically switch between light and dark theme
		augroup AppearanceChangedUserGroup
			autocmd!
			autocmd OSAppearanceChanged * call <SID>UpdateGuiMacvimColorscheme()
		augroup END

		return
	endif

	" Default colorscheme
	call <SID>SetDarkTheme()
endfunction

" =============================================================================
" Builtin plugins (BUILT)
" =============================================================================

packadd! matchit
packadd! editorconfig
packadd! comment
packadd! nohlsearch
packadd! hlyank

" =============================================================================
" Plugin Manger (PLUG)
" =============================================================================

let data_dir = expand('~/.vim')
if empty(glob(data_dir . '/autoload/plug.vim'))
	silent execute '!curl -fLo '.data_dir.'/autoload/plug.vim --create-dirs  https://raw.githubusercontent.com/junegunn/vim-plug/master/plug.vim'
	autocmd VimEnter * PlugInstall --sync | source $MYVIMRC
endif

call plug#begin()

" Deps
" ---
Plug 'lambdalisue/vim-nerdfont'
Plug 'vim-denops/denops.vim'
Plug 'vim-denops/denops-shared-server.vim'
Plug 'roxma/nvim-yarp'
Plug 'roxma/vim-hug-neovim-rpc'

" Core
" ---
Plug 'cohama/lexima.vim'
Plug 'creativenull/projectlocal-vim', { 'tag': 'v1.*' }
Plug 'mattn/emmet-vim'
Plug 'tpope/vim-abolish'
Plug 'tpope/vim-repeat'
Plug 'tpope/vim-surround'
Plug 'Shougo/context_filetype.vim'
Plug 'markonm/traces.vim', #{ commit: '9663fcf84de5776bee71b6c816c25ccb6ea11d1a' }

" File explorer
" ---
Plug 'lambdalisue/vim-fern'
Plug 'lambdalisue/vim-fern-renderer-nerdfont'
Plug 'lambdalisue/vim-glyph-palette'

" Fuzzy finder/Code finder
" ---
Plug 'junegunn/fzf', #{ tag: 'v0.67.*', do: { -> fzf#install()  }  }
Plug 'junegunn/fzf.vim'
Plug 'linrongbin16/fzfx.vim', #{ commit: '9bd93e78f22c734751688cefd3ee2c475cd85ccd'  }

" Git
" ---
Plug 'tpope/vim-fugitive'
Plug 'itchyny/vim-gitbranch'
Plug 'airblade/vim-gitgutter'

" Linters + Formatters
" ---
Plug 'dense-analysis/ale'

" Autocompletion
" ---

" UI/Aeshetics
" ---
Plug 'vim-airline/vim-airline'
Plug 'L-TChen/auto-dark-mode.vim'

" File syntax plugins
" ---

" Colorschemes
" ---
Plug 'bluz71/vim-moonfly-colors'
Plug 'bluz71/vim-nightfly-guicolors'
Plug 'rakr/vim-one'

call plug#end()

" =============================================================================
" Configure plugins (POST)
" =============================================================================

" fzf.vim Config
" ---
let $FZF_DEFAULT_COMMAND = 'rg --files --hidden --iglob !.git'
let $FZF_DEFAULT_OPTS = '--reverse --color=border:#aaaaaa,gutter:-1,bg+:-1'
let g:fzf_vim = #{
			\preview_window: [],
			\grep_multi_line: 1
			\}

command! -bang -nargs=* Rg call <SID>VimGrep(<q-args>, <bang>0)

nnoremap <C-p> <Cmd>Files<CR>
nnoremap <C-t> <Cmd>Rg<CR>
nnoremap <C-y> <Cmd>call <SID>VimGrep(expand('<cword>'), 0)<CR>
nnoremap <C-@> <Cmd>FzfxBuffers<CR>

autocmd! User FzfStatusLine call <SID>FzfStatusline()
autocmd! ColorScheme * highlight! fzfBorder guifg=#aaaaaa

" fern.vim Config
" ---
let g:fern#hide_cursor = 1
let g:fern#default_hidden = 1
let g:fern#renderer = 'nerdfont'
let g:fern#renderer#nerdfont#root_symbol = 'î­… '
let g:fern#renderer#nerdfont#indent_markers = 1

nnoremap <Leader>ff <Cmd>Fern . -reveal=%<CR>

autocmd! FileType fern call <SID>FernInit()

" emmet-vim Config
" ---
let g:user_emmet_leader_key = '<C-q>'
let g:user_emmet_mode = 'in'
let g:user_emmet_settings = #{
			\blade: #{ extends: 'html' },
			\}
imap <C-x><C-y> <Plug>(emmet-expand-abbr)
nmap <Leader>er <Plug>(emmet-update-tag)

" indentLine Config
" ---
let g:indentLine_char = 'â”‚'
let g:indentLine_fileTypeExclude = ['help', 'fern', 'fzf']

" vim-airline Config
" ---
let g:airline_experimental = 1
let g:airline_powerline_fonts = 1
let g:airline#extensions#ale#enabled = 1

let g:airline_section_z = 'â„…:%v, î‚¡:%L'

" hlyank Config
" ---
let g:hlyank_hlgroup = 'IncSearch'
let g:hlyank_duration = 300
let g:hlyank_invisual = 1

" =============================================================================
" Configure theme (THEME)
" =============================================================================

let g:moonflyNormalFloat = v:true
let g:moonflyTransparent = v:true

call <SID>ConfigTheme()
